# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tidal
pkgname=haskell-tidal
pkgver=2.0.0
pkgrel=6
pkgdesc="A domain specific language for live coding pattern."
arch=(x86_64)
url="https://tidalcycles.org/"
license=(GPL3)
groups=(pro-audio)
depends=(
  ghc
  ghc-libs
  glibc
  haskell-clock
  haskell-colour
  haskell-hosc
  haskell-mwc-random
  haskell-network
  haskell-primitive
  haskell-random
  haskell-tidal-link
  haskell-vector
  supercollider
)
checkdepends=(haskell-microspec)
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=(tidalcycles)
replaces=(tidalcycles)
source=(https://hackage.haskell.org/package/tidal-2.0.0/candidate/tidal-2.0.0.tar.gz)
sha512sums=('ff2f9e8a6b565a9eb2e9d704058389f433a032a521475d68ce52191545263ef63b3e5e57f94c6c5f58156e75c22741aa612618682c44232b50d7272e004d8e3f')
b2sums=('e3a82e790ea55ac5bb767370ad790dd20f91686749a3e2444026ec892b99ff5b9a85bcd3629ec8401f1c20abb1866a687613cdddb551e3dce30db2b2e3cb2a74')

build() {
  local configure_options=(
    -O
    --datasubdir=$pkgname
    --disable-library-vanilla
    --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname
    --dynlibdir=/usr/lib
    --enable-executable-dynamic
    --enable-shared
    --enable-tests
    --ghc-option=-fllvm
    --prefix=/usr
    --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  )

  cd $_name-$pkgver
  runhaskell Setup configure "${configure_options[@]}"
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_name-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_name-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}

